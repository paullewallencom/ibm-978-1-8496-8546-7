var num_sku_loc
var customer_location
var sku_number
var idx
var tmp_node

# delete all existing models
for tmp_node in ^stream.nodes
	if ^tmp_node.node_type = applytimeseriesnode then
		delete ^tmp_node
	endif
endfor

# create distinct list of SKUs and locations
execute sku_loc_table

set num_sku_loc = sku_loc_table.output.row_count

for idx from 1 to num_sku_loc
	set sku_number = value sku_loc_table.output at ^idx 2
	set customer_location = value sku_loc_table.output at ^idx 1

	# update select statement with new location and SKU
	set select_sku_loc.condition = "CUSTOMER_LOCATION_ID = " >< ^customer_location >< " and SKU_NUMBER = " >< ^sku_number
	
	if idx = 1 then
		set forecast_output.write_mode = Overwrite
	else 
		set forecast_output.write_mode = Append
	endif

	# build model
	execute time_series
	insert model time_series_model connected between intervals and replace_nulls

	# generate forecast
	execute forecast_output

	# delete time series model
	delete time_series_model
	clear outputs
endfor